plugins {
    id 'java'
    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.1.25"
    id "jacoco"
    id 'org.sonarqube' version "4.4.1.3373"
    id 'maven-publish'
}

group = 'com.example'
version = '1.0-SaadNamune'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

dependencies {
    testImplementation 'io.cucumber:cucumber-java:7.14.0'
    testImplementation 'io.cucumber:cucumber-junit:7.14.0'
    testImplementation 'junit:junit:4.13.2'
}

// Configuration Cucumber Reports
cucumberReports {
    outputDir = file('build/reports/cucumber/html')
    buildId = '0'
    reports = files('build/reports/cucumber/cucumber.json')
}

// Configuration JaCoCo
jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = true
        html.destination = file("${buildDir}/reports/jacoco")
    }
}

// Configuration SonarQube
sonar {
    properties {
        property "sonar.projectKey", "tp"
        property "sonar.projectName", "TP Jenkins Pipeline"
        property "sonar.host.url", System.getenv("SONAR_HOST_URL") ?: "http://localhost:9000"
        property "sonar.token", System.getenv("SONAR_TOKEN") ?: ""
        property "sonar.sources", "src/main/java"
        property "sonar.tests", "src/test/java"
        property "sonar.java.binaries", "build/classes"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
    }
}

// Configuration Publication Maven
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'NamuneSaad'
            artifactId = 'my-library'
            version = '1.0.0'
        }
    }
    repositories {
        maven {
            name = "myrepo"
            url = uri(System.getenv("MAVEN_REPO_URL") ?: "https://example.com/repo/")
            credentials {
                username = System.getenv("MAVEN_REPO_USERNAME") ?: "user"
                password = System.getenv("MAVEN_REPO_PASSWORD") ?: "password"
            }
        }
    }
}

// Tâche pour générer la documentation
javadoc {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    destinationDir = file("$buildDir/docs/javadoc")
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
}

// Configuration des tests
test {
    useJUnit()

    // Configuration pour générer les rapports Cucumber en JSON
    systemProperty "cucumber.plugin", "json:build/reports/cucumber/cucumber.json"

    finalizedBy jacocoTestReport

    // Génère les rapports même si les tests échouent
    ignoreFailures = false
}

// La tâche sonar dépend des tests
tasks.named('sonar') {
    dependsOn test, jacocoTestReport
}